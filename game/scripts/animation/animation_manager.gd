class_name AnimationManager
extends RefCounted

enum State { GROUNDED, INITIATE_JUMP, JUMP_UP, JUMP_DOWN, LANDING }

const DEFAULT_BLEND: float = 0.1
const LANDING_BLEND: float = 0.05

var animation_player: AnimationPlayer
var body: Node3D
var current_state: State = State.GROUNDED
var was_on_floor: bool = true
var facing_right: bool = true


func _init(anim_player: AnimationPlayer, body_node: Node3D):
	animation_player = anim_player
	body = body_node


func update(velocity: Vector3, is_on_floor: bool) -> void:
	_update_facing(velocity.x)

	var new_state = _determine_state(velocity.y, is_on_floor)

	if new_state != current_state:
		_transition_to(new_state)

	var is_idle = abs(velocity.x) < 0.1 and is_on_floor
	if current_state == State.GROUNDED:
		animation_player.speed_scale = 0.0 if is_idle else 1.0

	was_on_floor = is_on_floor


func _update_facing(velocity_x: float) -> void:
	if velocity_x > 0.1 and not facing_right:
		facing_right = true
		body.scale.x = 1.0
	elif velocity_x < -0.1 and facing_right:
		facing_right = false
		body.scale.x = -1.0


func _determine_state(velocity_y: float, is_on_floor: bool) -> State:
	if is_on_floor:
		if current_state == State.JUMP_DOWN:
			return State.LANDING
		if current_state == State.LANDING and animation_player.is_playing():
			return State.LANDING
		return State.GROUNDED

	if current_state == State.GROUNDED and velocity_y > 0:
		return State.INITIATE_JUMP

	if current_state == State.INITIATE_JUMP:
		return State.JUMP_UP if not animation_player.is_playing() else State.INITIATE_JUMP

	return State.JUMP_UP if velocity_y > 0 else State.JUMP_DOWN


func _transition_to(new_state: State) -> void:
	current_state = new_state
	animation_player.speed_scale = 1.0

	match new_state:
		State.GROUNDED:
			animation_player.play("walk", DEFAULT_BLEND)
		State.INITIATE_JUMP:
			animation_player.play("initate_jump", DEFAULT_BLEND)
		State.JUMP_UP:
			animation_player.play("jump_up", DEFAULT_BLEND)
		State.JUMP_DOWN:
			animation_player.play("jump_down", DEFAULT_BLEND)
		State.LANDING:
			animation_player.play("jump_land", LANDING_BLEND)
